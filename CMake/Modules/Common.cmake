set(CMAKE_DEBUG_POSTFIX "_d" CACHE STRING "Add a postfix, usually _d on windows")
set(CMAKE_RELEASE_POSTFIX "" CACHE STRING "Add a postfix, usually empty on windows")
set(CMAKE_RELWITHDEBINFO_POSTFIX "" CACHE STRING "Add a postfix, usually empty on windows")
set(CMAKE_MINSIZEREL_POSTFIX "" CACHE STRING "Add a postfix, usually empty on windows")

function(DownloadFile url dest_file file_hash)
    set(redownload FALSE)
    if(EXISTS ${dest_file})
        file(SHA1 ${dest_file} file_sha1)
        string(TOLOWER "${file_sha1}" file_sha1)
        string(TOLOWER "${file_hash}" file_hash)
        if(NOT "${file_sha1}" STREQUAL ${file_hash})
            set(redownload TRUE)
        endif()
    else()
        set(redownload TRUE)
    endif()
    if(redownload)
        message(STATUS "Downloading ${url}...")
        file(DOWNLOAD ${url} ${dest_file} EXPECTED_HASH SHA1=${file_hash} STATUS err)
        list(GET err 0 err_code)
        if(err_code)
            file(REMOVE ${dest_file})
            list(GET err 1 err_msg)
            message(FATAL_ERROR "Failed to download file ${url}: ${err_msg}")
        endif()
    endif()
    set(${ARGV3} ${redownload} PARENT_SCOPE)
endfunction()
