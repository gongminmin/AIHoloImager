// Copyright (c) 2025 Minmin Gong
//

#pragma once

#include "Platform.hlslh"

void AtomicAdd(RWBuffer<uint32_t> buff, uint addr, float value)
{
    uint32_t cmp_val = buff[addr];
    ALLOW_UAV_CONDITION
    for (;;)
    {
        uint32_t new_val = asuint(asfloat(cmp_val) + value);
        uint32_t ori_val;
        InterlockedCompareExchange(buff[addr], cmp_val, new_val, ori_val);
        if (cmp_val == ori_val)
        {
            break;
        }
        cmp_val = ori_val;
    }
}

template <typename T>
void Swap(inout T a, inout T b)
{
    T tmp = a;
    a = b;
    b = tmp;
}

float Cross(float2 lhs, float2 rhs)
{
    return lhs.x * rhs.y - lhs.y * rhs.x;
}

struct SilhouetteInfo
{
    uint32_t2 coord;
    bool is_up;
    bool is_face1;
    uint32_t edge;
    float alpha;
};

void EncodeSilhouetteInfo(RWBuffer<uint32_t> silhouette_info, uint32_t index, SilhouetteInfo sil_info)
{
    const uint32_t offset = index * 2;

    silhouette_info[offset + 0] = (sil_info.edge << 30) | (sil_info.coord.x << 16) | ((sil_info.is_face1 ? 1 : 0) << 15) | ((sil_info.is_up ? 1 : 0) << 14) | sil_info.coord.y;
    silhouette_info[offset + 1] = asuint(sil_info.alpha);
}

SilhouetteInfo DecodeSilhouetteInfo(Buffer<uint32_t> silhouette_info, uint32_t index)
{
    SilhouetteInfo sil_info;

    const uint32_t offset = index * 2;
    const uint32_t encoded = silhouette_info[offset + 0];

    sil_info.coord.x = (encoded >> 16) & 0x3FFFu;
    sil_info.coord.y = encoded & 0x3FFFu;

    sil_info.is_up = (encoded >> 14) & 1u;
    sil_info.is_face1 = (encoded >> 15) & 1u;
    sil_info.edge = (encoded >> 30) & 3u;

    sil_info.alpha = asfloat(silhouette_info[offset + 1]);

    return sil_info;
}

uint32_t DivUp(uint32_t a, uint32_t b)
{
    return (a + b - 1) / b;
}

float2 VpToNdc(uint32_t2 win_coord, float4 viewport)
{
    float2 ndc_coord = (win_coord + 0.5f - viewport.xy) / viewport.zw * 2 - 1;
    ndc_coord.y = -ndc_coord.y;
    return ndc_coord;
}

void RightUpCoord(uint32_t2 center_coord, uint32_t2 tex_size, out uint32_t2 right_coord, out uint32_t2 up_coord)
{
    right_coord = uint32_t2(min(center_coord.x + 1, tex_size.x - 1), center_coord.y);
    up_coord = uint32_t2(center_coord.x, max(center_coord.y, 1) - 1);
}

uint32_t HashFunc(uint32_t2 val)
{
    uint32_t h = val.x ^ val.y;
    h ^= h >> 16;
    h *= 0x85EBCA6BU;
    h ^= h >> 13;
    h *= 0xC2B2AE35U;
    h ^= h >> 16;
    return h;
}

float2 DuvDxyw(float2 bc, float da0_di, float da1_di, float da2_di, float inv_area)
{
    return (float2(da0_di, da1_di) - bc * (da0_di + da1_di + da2_di)) * inv_area;
}

uint32_t MipLevelOffset(uint32_t4 mip_level_offsets[4], uint32_t level)
{
    const uint32_t index = level / 4;
    const uint32_t comp = level - index * 4;
    return mip_level_offsets[index][comp];
}

uint32_t2 MipLevelSize(uint32_t2 tex_size, uint32_t level)
{
    return max(tex_size >> level, 1);
}

float CalcMipLevel(float4 duv_dxy, uint32_t2 tex_size, uint32_t mip_levels)
{
    const float2 duv_dx = duv_dxy.xz * tex_size;
    const float2 duv_dy = duv_dxy.yw * tex_size;
    const float d = max(dot(duv_dx, duv_dx), dot(duv_dy, duv_dy));
    const float level = log2(sqrt(d));
    return clamp(level, 0, mip_levels - 1);
}
